.options savecurrents

* PHILIPS BJT'S
.MODEL BC557A PNP(IS=2.059E-14 ISE=2.971f ISC=1.339E-14 XTI=3 BF=227.3 BR=7.69 IKF=0.08719 IKR=0.07646 XTB=1.5 VAF=37.2 VAR=11.42 VJE=0.5912 VJC=0.1 RE=0.688 RC=0.6437 RB=1 RBM=1 IRB=1E-06 CJE=1.4E-11 CJC=1.113E-11 XCJC=0.6288 FC=0.7947 NF=1.003 NR=1.007 NE=1.316 NC=1.15 MJE=0.3572 MJC=0.3414 TF=7.046E-10 TR=1m2 ITF=0.1947 VTF=5.367 XTF=4.217 EG=1.11)
.MODEL BC547A NPN(IS=1.533E-14 ISE=7.932E-16 ISC=8.305E-14 XTI=3 BF=178.7 BR=8.628 IKF=0.1216 IKR=0.1121 XTB=1.5 VAF=69.7 VAR=44.7 VJE=0.4209 VJC=0.2 RE=0.6395 RC=0.6508 RB=1 RBM=1 IRB=1E-06 CJE=1.61E-11 CJC=4.388p XCJC=0.6193 FC=0.7762 NF=1.002 NR=1.004 NE=1.436 NC=1.207 MJE=0.3071 MJC=0.2793 TF=4.995E-10 TR=1m2 ITF=0.7021 VTF=3.523 XTF=139 EG=1.11)

Vcc vcc 0 12.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)
Rin in in2 100

* input coupling capacitor
Ci in2 base 5u

* bias circuit
R1 vcc base 132k
R2 base 0 10k

* gain stage
Q1 coll base emit BC547A
Rc vcc coll 3.4k
Re emit 0 50

* bypass capacitor
Cb emit 0 100u


* output stage
Vbase coll base2 0
Q2 coll2 base2 emit2 BC557A
Vcoll coll2 0 0
Rout emit2 vcc 360

* output coupling capacitor
Co emit2 out 1u

* load
Vout out out2 0
RL out2 0 8

.op
.end

.control
set hcopypscolor=1
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0
set units=degrees

op
echo "op1_TAB"
print all
echo "op1_END"

echo  "op2_TAB"
let ib1 = (v(vcc)-v(base))/132000 + (v(in)-v(in2))/100 - v(base)/10000
let ic1 = (v(vcc)-v(coll))/3400 - i(Vbase)
let vo1 = v(vcc)-3400*ic1
let ie1 = v(emit)/50
let ve1 = 50*ie1
let vce = vo1 - ve1
let ie2 = (v(vcc)-v(emit2))/360 + v(out)/8
let ic2 = i(Vcoll)
let vo2 = 12 - 360*ie2

print ib1
print ic1
print vo1
print ie1
print ve1
print vce
print ie2
print ic2
print vo2
echo  "op2_END"
print all

* time analysis
tran 1e-5 1e-2
*plot v(coll)
hardcopy vo1.ps vdb(coll)
hardcopy vout.ps v(out) 


* frequency analysis
ac dec 10 10 100MEG
*plot vdb(coll)
*plot vp(coll)
hardcopy vo1f.ps vdb(coll)

*plot vdb(out)
*plot vp(out)
let cutoff = vecmax(vdb(out))-3
meas ac f1 WHEN vdb(out)=cutoff RISE=1
meas ac f2 WHEN vdb(out)=cutoff CROSS=LAST
let bandwidth = f2-f1
let gainlow = abs(v(out)[6]/v(in)[6])
let gainband = abs(v(out)[40]/v(in)[40])
let gainhigh = abs(v(out)[46]/v(in)[46])
let cost = 251.918
let merit = (bandwidth*gainband)/(f1*cost)
hardcopy vo2f_m.ps vdb(out) cutoff
hardcopy vo2f_ph.ps vp(out)
hardcopy tf.ps {vdb(out)-vdb(in)}

*input impedance in kohm
let inputimpedance = 100*v(in)[40]/(v(in)[40]-v(in2)[40])/1000

*output impedance in kohm
let outputimpedance = v(out)[40]/i(vout)[40]

echo  "out_TAB"
print inputimpedance
print outputimpedance
print bandwidth
print gainlow
print gainband
print gainhigh
print merit

echo  "out_END"


.endc 

