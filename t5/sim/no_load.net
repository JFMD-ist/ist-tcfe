*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 5.0
Vee vee 0 -5.0
Vin check 0 0 ac 10m sin(0 10m 1k)
Vcheck check in 0 

X1 0 inv_in vcc vee out uA741

*High-Pass Stage
C1 in in1 2440n
R1 in1 0 30k

*Z1
R2 in1 inv_in 1000

*Z2
R3 inv_in out1 100k

*Low-Pass Stage
R4 out1 outc 2000
C2 out 0 220n
Vout outc out 0

.op
.end

.control
set hcopypscolor=1
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0
set units=degrees

print all

* time analysis
tran 1e-5 1e-2
*plot v(out)
hardcopy vo1.ps vdb(out)
*plot v(out)
hardcopy vo1.ps v(out)


* frequency analysis
ac dec 10 10 100MEG
*plot vdb(out)
*plot vp(out)

let cutoff = vecmax(vdb(out)) - 3
meas ac f1 WHEN vdb(out)=cutoff RISE=1
meas ac f2 WHEN vdb(out)=cutoff CROSS=LAST
let central_freq = sqrt(f1*f2)
let gain = abs(v(out)[20]/v(in)[20])
print central_freq
print gain
let freq_dev = abs(central_freq - 1000)
let gain_dev = abs(gain - 100)
print v(in)[0]
print v(in)[20]
print v(out)[0]
print v(out)[20]
print freq_dev
print gain_dev
let cost = 13323.2920387 + 135.66
let merit = 1/(cost*(gain_dev + freq_dev + 0.000001))
print merit

hardcopy vout_mag.ps vdb(out) cutoff
hardcopy vout_ph.ps vp(out)
hardcopy gain.ps abs(v(out)/v(in)) cutoff

*input impedance in kohm
print v(in)[20]/i(Vcheck)[20]/1000


*output impedance in kohm
print v(out)[20]/i(Vout)[20]/1000

.endc 

